/* Converts a markdown document to HTML

   Known limitations:
   * Doesn't handle lists well
       * Doesn't handle nested lists
   * Can't do links with references.
   * Doesn't handle not parsing things inside of code blocks.
   * Assumes wellformedness of formatting tags. (I.e., doesn't check if ** is balanced.)
*/
start: large >> small >> references >> formatting

large := (markdownElement /\n/)*

small := (code | /./)*

references := (image | link | /./)*

markdownElement :=
     header
   | list
   | blockquote
   | fancyHeader
   | horizontalRule
   | catchall

header := ~/#{6} / "<h6>" line "</h6>"
       |  ~/#{5} / "<h5>" line "</h5>"
       |  ~/#{4} / "<h4>" line "</h4>"
       |  ~/#{3} / "<h3>" line "</h3>"
       |  ~/#{2} / "<h2>" line "</h2>"
       |  ~/# /    "<h1>" line "</h1>"

fancyHeader := "<h1>" line "</h1>" ~/\n/ ~/={3,}/
            |  "<h2>" line "</h2>" ~/\n/ ~/-{3,}/

list := "<ul>" unorderedList "</ul>"
      | "<ol>" orderedList "</ol>"

unorderedList := unorderedListLine /\n/ unorderedList
              |  unorderedListLine

unorderedListLine := ~/[*+-] / "<li>" continuedLine "</li>"

orderedList := orderedListLine /\n/ orderedList
            |  orderedListLine

orderedListLine := ~/[0-9]+\. / "<li>" continuedLine "</li>"

blockquote := "<blockquote>" blockquoteBlock "</blockquote>"

blockquoteBlock := blockquoteLine /\n/ blockquoteBlock
                |  blockquoteLine

blockquoteLine := ~/> / line
               |  ~/>/

horizontalRule := ~/ *(- *){3,}/ "<hr/>"
               |  ~/ *(\* *){3,}/ "<hr/>"

catchall := line

continuedLine := line ~/\n {2,}/ " " continuedLine
              |  line

line := /[^\n]*/

initFormat := boldp@"/" emp@"/" strikep@"/"

swapBold   := tmp@(!boldp) boldp@(!bolda) bolda@(!tmp)
swapEm     := tmp@(!emp) emp@(!ema) ema@(!tmp)
swapStrike := tmp@(!strikep) strikep@(!strikea) strikea@(!tmp)

code :=  ~/``/ "<pre>" /([^\n`]|`[^\n`])*/ "</pre>" ~/``/
      |  ~/`/  "<pre>" /[^\n`]*/ "</pre>" ~/`/

// ![foo](http://bar) -> <img alt="foo" src="http://bar"/>
image := ~/!/ "<img alt=\"" ~/\[/ /([^]]|\\.)*/ ~/]/ "\" src=\"" ~/\(/ /([^)]|\\.)*/ ~/\)/ "\"/>"

// [foo](http://bar) -> <a href="http://bar">foo</a>
link := ~/\[/ linktitle@/([^]]|\\.)*/ ~/]/ "<a href=\"" ~/\(/ /([^)]|\\.)*/ ~/\)/ "\"/>" !linktitle "</a>"

formatting := initFormat (format | /./)*

format := ~/\*\*/ "<" !bolda   "strong>" swapBold
       |  ~/__/   "<" !bolda   "strong>" swapBold
       |  ~/\*/   "<" !ema     "em>"     swapEm
       |  ~/_/    "<" !ema     "em>"     swapEm
       |  ~/~~/   "<" !strikea "strike>" swapStrike
