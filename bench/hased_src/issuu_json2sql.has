/* Convert (Issuu) JSON to (Issuu) SQL insert statements. */

start := "INSERT INTO issuu_log (ts, visitor_uuid, visitor_useragent, visitor_country, env_doc_id, subject_doc_id) VALUES\n" json2sql

json2sql := object ",\n" ws json2sql
          | object ";\n" ws

object := "(" ~<\{> ws keyVals ws ~<}> ")"

keyVals := ws keyVal keyVals
         | ws keyVal

keyVal := ~<"ts">                sep someInt             keepComma
        | ~<"visitor_uuid">      sep stringReplaceQuotes keepComma
        | ~<"visitor_useragent"> sep stringReplaceQuotes keepComma
        | ~<"visitor_country">   sep stringReplaceQuotes keepComma
        | ~<"env_doc_id">        sep stringReplaceQuotes keepComma
        | ~<"subject_doc_id">    sep stringReplaceQuotes dropComma
        | ~(<"> someString <">   sep ( <"> someString <">
                                     | someInt
                                     ) (dropComma | ""))


stringReplaceQuotes := qt someString qt
qt := "'" ~<"> // replace double with single quote
sep := ws ~<:> ws
someString := <[^"\n]*>
someInt := <[0-9]*>
someNumber := someInt <\.> someInt
contryCode := <[A-Z]{2}>

// Skip whitespace
ws := ~<[ \n]*>
keepComma := ws <,>
dropComma := ws ~<,>
